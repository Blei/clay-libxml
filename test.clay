import io.files.(stderr);
import libxml.*;
import printer.(
    println,
    printlnTo);

private parseStoryinfo(doc, cur) {
    for (node in XmlNodePtrSequence(cur^.children)) {
        if (xmlStrcmp(node^.name, xmlString("keyword")) == 0) {
            var keyword = nodeListGetString(doc, node^.children);
            println("keyword is '", keyword, "'");
        }
    }
}

private parseDoc(docName) {
    println("parsing ", docName, "...");

    var doc = xmlParseFile(cstring(docName));
    if (null?(doc)) {
        printlnTo(stderr, "Parsing failed");
        return;
    }

    var cur = xmlDocGetRootElement(doc);

    if (null?(cur)) {
        printlnTo(stderr, "Empty document");
        xmlFreeDoc(doc);
        return;
    }
    
    if (xmlStrcmp(cur^.name, xmlString("story")) != 0) {
        printlnTo(stderr, "Document is of the wrong type, root node != story");
        xmlFreeDoc(doc);
        return;
    }

    for (node in XmlNodePtrSequence(cur^.children)) {
        if (xmlStrcmp(node^.name, xmlString("storyinfo")) == 0) {
            parseStoryinfo(doc, node);
        }
    }

    xmlFreeDoc(doc);
}

main(argc, argv): Int {

    if (argc <= 1) {
        println("Usage: ", CStringRef(argv[0]), " docname");
        return 1;
    }

    var docname = CStringRef(argv[1]);
    parseDoc(docname);

    return 0;
}
